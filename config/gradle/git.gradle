import java.text.SimpleDateFormat

def execCmd = { command ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine command
        standardOutput = stdout
    }
    return stdout.toString().trim()
}


def getGitHash = { ->
    return execCmd(['git', 'rev-parse', '--short', 'HEAD'])
}

// git rev-parse --abbrev-ref HEAD
def getBranchName = { ->
    return execCmd(['git', 'rev-parse', '--abbrev-ref', 'HEAD'])
}

def getGitDate = { ->
    def logDate = execCmd(['git', 'log', '-1', '--format=%cd'])
    SimpleDateFormat parser = new SimpleDateFormat("EEE MMM d HH:mm:ss yyyy");
    Date date = parser.parse(logDate);
    SimpleDateFormat formatter = new SimpleDateFormat("yyyyMMdd_HHmmss");
    return formatter.format(date);
}

ext.latestGitHash = getGitHash()
ext.latestGitDate = getGitDate()
ext.currentBranch = getBranchName()


