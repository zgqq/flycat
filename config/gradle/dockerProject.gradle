ext.jvmArgs = applicationDefaultJvmArgs
jib {
    from {
//        image = 'amazoncorretto:8u232'
        image = 'zgqq/flycat:latest'
    }

    extraDirectories {
        paths = [file('build/jib')]
    }

    to {
        image = dockerImage
        credHelper = 'docker-credential-osxkeychain'
        tags = [env.toString() + '-' + (new Date().format("yyyyMMdd_HHmmss")).toString()]
        auth {
            println "image  $dockerImage username $dockerUsername"
            username = dockerUsername
            password = dockerPassword
        }
    }

    container {
        jvmFlags = jvmArgs
        mainClass = mainClassName
        ports = [containerPort]
        volumes = ['/app/data/logs']
        creationTime = 'USE_CURRENT_TIMESTAMP'
    }
//    allowInsecureRegistries = true
}


def updateServer(serverRole, execPath) {
    if (remotes.role(serverRole)) {
        ssh.run {
            session(remotes.role(serverRole)) {
                def gitRepo = "/home/${remote.user}/git-repo/${project.name}/"
                def dockerDir = gitRepo + "deploy/docker/"
                def updateScript = execPath
                executeScript """
                        cd ${gitRepo}
                        git pull 
                        cd ${dockerDir}
                        sh ${updateScript}
                """
                println "Deployed to ${remote.user}@${remote.host}:${gitRepo}"
            }
        }
    }
}

def updateSh = "./update_production.sh"

task dockerRestartProd() {
    doLast {
        def serverRole = 'production'
        updateServer(serverRole, updateSh)
        println "Restarted prod server"
    }
}

task dockerUpdateProd() {
    dependsOn 'jib'
    doLast {
        def serverRole = 'production'
        updateServer(serverRole, updateSh)
        println "Updated prod server"
    }
}

def rollbackSh = "./rollback_production.sh"
task dockerRollbackProd() {
    dependsOn 'jib'
    doLast {
        def serverRole = 'production'
        updateServer(serverRole, rollbackSh)
        println "Updated prod server"
    }
}

task setupExtraDir {
    doLast {
        copy {
            from file('build/generated/git-diff')
            into file('build/jib/project/generated/')
        }
        copy {
            from file('src')
            into file('build/jib/project/src/')
        }
        copy {
            from file('build.gradle'), file('settings.gradle'), file('deploy')
            into file('build/jib/project/')
        }
    }
}


tasks.jib.dependsOn tasks.build, tasks.setupExtraDir

tasks.jibDockerBuild.dependsOn tasks.build, tasks.setupExtraDir
