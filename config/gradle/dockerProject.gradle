ext.jvmArgs = applicationDefaultJvmArgs
jib {
    from {
//        image = 'amazoncorretto:8u232'
        image = 'tomcat:9.0-jdk8-corretto'
    }

    to {
        image = dockerImage
        credHelper = 'docker-credential-osxkeychain'
        tags = [env.toString() + '-' + (new Date().format("yyyyMMdd_HHmmss")).toString()]
        auth {
            println "image  $dockerImage username $dockerUsername"
            username = dockerUsername
            password = dockerPassword
        }
    }

    container {
        jvmFlags = jvmArgs
        mainClass = mainClassName
        ports = [containerPort]
        volumes = ['/app/data/logs']
        creationTime = 'USE_CURRENT_TIMESTAMP'
    }
//    allowInsecureRegistries = true
}

def updateServer(serverRole) {
    if (remotes.role(serverRole)) {
        ssh.run {
            session(remotes.role(serverRole)) {
                def gitRepo = "/home/${remote.user}/git-repo/${project.name}/"
                def dockerDir = gitRepo + "deploy/docker/"
                def updateScript = "./update_production.sh"
                executeScript """
                        cd ${gitRepo}
                        git pull 
                        cd ${dockerDir}
                        sh ${updateScript}
                """
                println "Deployed to ${remote.user}@${remote.host}:${gitRepo}"
            }
        }
    }
}

task dockerRestartProd() {
    doLast {
        def serverRole = 'production'
        updateServer(serverRole)
        println "Restarted prod server"
    }
}

task dockerUpdateProd() {
    dependsOn 'jib'
    doLast {
        def serverRole = 'production'
        updateServer(serverRole)
        println "Updated prod server"
    }
}

tasks.jib.dependsOn tasks.build